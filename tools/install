;; -*- scheme -*-

(define *manifest*
  '((program "bin"    "tap-harness")
    (module  "scheme" "test/tap.scm")
    (module  "scheme" "test/tap-harness.scm")))

(use-modules (ice-9 match)
             (ice-9 optargs))

(define (make-default . lst)
  (let ((value lst))
    (case-lambda
      (() (string-concatenate (map (lambda (obj)
                                     (if (procedure? obj) (obj) obj))
                                   value)))
      (l (set! value l)))))

(define *destdir*       (make-default ""))
(define *prefix*        (make-default "/usr/local"))
(define *bindir*        (make-default *prefix* "/bin"))
(define *libdir*        (make-default *prefix* "/lib"))
(define *datarootdir*   (make-default *prefix* "/share"))
(define *guile-version* (make-default (effective-version)))
(define *moduledir*     (make-default *datarootdir* "/guile/site/" *guile-version*))
(define *objectdir*     (make-default *libdir* "/guile/" *guile-version* "/site-ccache"))
(define dryrun? #f)

(define (strip-scm scm)
  (let ((n (string-length scm))
        (m (string-length ".scm")))
    (if (< m n)
        (substring scm 0 (- n m))
        scm)))

(define (add-go f)
  (string-append f ".go"))

(define (maybe-variable str modify!)
  (lambda (s)
    (let* ((pf (string-append str "="))
           (n (string-length pf)))
      (if (string-prefix? pf s)
          (begin (modify! (substring s n))
                 #t)
          #f))))

(define-syntax-rule (cmdline-variable str sym)
  (maybe-variable str (lambda (s) (sym s))))

(define (usage)
  (format #t "
usage: install [OPTION(s)...] [PARAMETER(s)...]

Options:
D
  --help, -h       Display this help text.
  --dry-run, -d    Run, but don't modify the file system.

Parameters:

  DESTDIR
  PREFIX
  BINDIR
  LIBDIR
  DATAROOTDIR
  GUILE_VERSION
  MODULEDIR
  OBJECTDIR

")
  (quit 0))

(define cmdline-processors
  (list (lambda (s)
          (if (or (string= s "--help")
                  (string= s "-h"))
              (usage)
              #f))
        (lambda (s)
          (if (or (string= s "--dry-run")
                  (string= s "-d"))
              (begin (set! dryrun? #t)
                     #t)
              #f))
        (cmdline-variable "DESTDIR"       *destdir*)
        (cmdline-variable "PREFIX"        *prefix*)
        (cmdline-variable "BINDIR"        *bindir*)
        (cmdline-variable "LIBDIR"        *libdir*)
        (cmdline-variable "DATAROOTDIR"   *datarootdir*)
        (cmdline-variable "GUILE_VERSION" *guile-version*)
        (cmdline-variable "MODULEDIR"     *moduledir*)
        (cmdline-variable "OBJECTDIR"     *objectdir*)))

(define (cmdline-process lst)
  (for-each (lambda (str)
              (let loop ((rest cmdline-processors))
                (unless (null? rest)
                  (unless ((car rest) str)
                    (loop (cdr rest))))))
            lst))

(cmdline-process (cdr (command-line)))

(format #t "bindir   : ~a~%" (*bindir*))
(format #t "moduledir: ~a~%" (*moduledir*))
(format #t "objectdir: ~a~%" (*objectdir*))

(define (dir? d)
  (and (file-exists? d)
       (file-is-directory? d)))

(define (system! . cmd)
  (let ((rc (apply system* cmd)))
    (unless (zero? rc)
      (format #t "System command failed. ~a~%" cmd)
      (format #t "Giving up!~%")
      (quit 1))))

(define (install-normal-file f d)
  (unless (dir? d)
    (unless dryrun?
      (system! "mkdir" "-p" d)
      (system! "chmod" "0755" d)))
  (let ((final (string-append (*destdir*) d "/" (basename f))))
    (format #t "Installing: ~a~%" final)
    (unless dryrun?
      (system! "cp" f final)
      (system! "chmod" "0644" final))))

(define* (install-with-prefix source-dir source-file destination-generate
                              #:optional prefix)
  (let ((destination (if prefix
                         (string-append (destination-generate) "/" prefix)
                         (destination-generate))))
    (install-normal-file (string-append source-dir "/" source-file)
                         destination)))

(define (install m)
  (match m
    (('program dir file) (install-with-prefix dir file *bindir*))
    (('module dir file)
     (let* ((module-prefix (dirname file))
            (module-name (basename file ".scm"))
            (object-file (string-append module-prefix "/" module-name ".go")))
       (install-with-prefix dir file        *moduledir* module-prefix)
       (install-with-prefix dir object-file *objectdir* module-prefix)))))

(for-each install *manifest*)
